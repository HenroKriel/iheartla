@@grammar::LA
@@whitespace :: /(?!.*)/     #parse whitespace manually
@@left_recursion::True

start
    =
    all_content $
    ;


all_content::AllContent
    = {left:single_line_separator} right:single_line_statement
    | left:{single_line_separator}
    ;


single_line_separator::SingleLineSeparator
    = value:single_line_statement {separator}+
    ;


single_line_statement::SingleLineStatement
    = {hspaces} value:statement {hspaces}
    ;

statement
    =
    | assignment
    | expression
    ;


separator
    = line | ';' {hspace}
    ;

expression
    =
    | addition
    | subtraction
    | term
    | {}
    ;

assignment::Assignment
    =
    left:identifier {hspaces} '=' {hspaces} right:expression {hspaces}
    ;


addition::Add
    =
    left:term {hspaces} op:'+' ~ {hspaces} right:expression
    ;


subtraction::Subtract
    =
    left:term {hspaces} op:'-' ~ {hspaces} right:expression
    ;


term
    =
    | multiplication
    | division
    | factor
    ;


multiplication::Multiply
    =
    left:term op:'*' right:term
    | left:term {hspaces} right:term
    ;


division::Divide
    =
    #left:term {hspaces} op:'/' ~ {hspaces} right:term
    left:factor {hspaces} op:'/' ~ {hspaces} right:term
    ;


factor
    =
    | subexpression
    | identifier
    | number
    | matrix
    ;

hspace
    = ' ' | '\t'
    ;

hspaces
    = {hspace}+
    ;

line
    = '\n' | '\r' | '\f'
    ;

lines
    = {line}+
    ;

subexpression::Subexpression
    =
    '(' {hspaces} ~ value:expression {hspaces} ')'
    ;


identifier
    = /[A-Za-z]/
    ;

#matrix
matrix
    = '[' {hspaces} rows {hspaces} ']'
    ;

rows
    =
    | rows {hspaces} separator {hspaces} row {hspaces}
    | rows {hspaces} separator {hspaces}
    | row {hspaces}
    ;


row
    =
    | row_with_commas {hspaces} expr_in_matrix
    | row_with_commas
    | expr_in_matrix
    ;

row_with_commas
    =
    | row_with_commas {hspaces} expr_in_matrix (',' | {hspaces}+)
    | expr_in_matrix (',' | {hspaces}+)
    ;

expr_in_matrix
    =
    | addition_in_matrix
    | subtraction_in_matrix
    | term_in_matrix
    | {}
    ;

addition_in_matrix
    =
    left:term_in_matrix {hspaces} op:'+' ~ {hspaces} right:expr_in_matrix
    ;


subtraction_in_matrix
    =
    left:term_in_matrix {hspaces} op:'-' ~ {hspaces} right:expr_in_matrix
    ;


term_in_matrix
    =
    | multiplication_in_matrix
    | division_in_matrix
    | factor
    ;

multiplication_in_matrix
    =
    factor {hspaces} '*' ~ {hspaces} term_in_matrix
    ;

division_in_matrix
    =
    left:factor {hspaces} op:'/' ~ {hspaces} right:term_in_matrix
    ;


# number definition
digit
    =
    /\d/
    ;

integer
    =
    {digit}+
    ;

exponent
    = /[DdEe][+-]?/ {digit}+
    ;

mantissa
    = ({digit}* '.' {digit}+) | ({digit}+ '.')
    ;

floating_point
    = mantissa [exponent]
    ;

double
    = integer exponent
    | floating_point
    ;

number
    =
    | double
    | integer
    ;



