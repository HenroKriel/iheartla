@@grammar::LA
@@whitespace :: /(?!.*)/     #parse whitespace manually
@@left_recursion::True

start
    =
    all_content $
    ;

#include :: "linear_algebra/la_grammar/keywords.ebnf"
#include :: "linear_algebra/la_grammar/number.ebnf"
#include :: "linear_algebra/la_grammar/operators.ebnf"
#include :: "linear_algebra/la_grammar/matrix.ebnf"
#include :: "linear_algebra/la_grammar/base.ebnf"

all_content::AllContent
    = {left:single_line_separator} right:single_line_statement
    | left:{single_line_separator}
    ;


single_line_separator::SingleLineSeparator
    = value:single_line_statement {separator}+ {hspaces}
    ;


single_line_statement::SingleLineStatement
    = {hspaces} value:statement {hspaces}
    ;

statement
    =
    | assignment
    | expression
    ;

expression
    =
    | addition
    | subtraction
    | term
    | operations
    | {}
    ;

assignment::Assignment
    =
    | left:identifier {hspace} '=' {hspace} right:expression {hspace}
    | left:identifier '=' right:expression {hspace}
    ;

term
    =
    | multiplication
    | division
    | factor
    ;

factor
    =
    | subexpression
    | identifier
    | number
    | matrix
    ;

subexpression::Subexpression
    =
    '(' {hspaces} value:expression {hspaces} ')'
    ;