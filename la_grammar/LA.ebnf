@@grammar::LA
@@whitespace :: /(?!.*)/     #parse whitespace manually
@@left_recursion::True

start::Start
    =
    {separator_with_space} stat:statements {separator_with_space}+ WHERE {separator_with_space}+ cond:where_conditions {blank} $
    ;

#include :: "linear_algebra/la_grammar/keywords.ebnf"
#include :: "linear_algebra/la_grammar/number.ebnf"
#include :: "linear_algebra/la_grammar/operators.ebnf"
#include :: "linear_algebra/la_grammar/matrix.ebnf"
#include :: "linear_algebra/la_grammar/base.ebnf"



statements::Statements
    =
    | {hspace} stats:statements {separator_with_space}+ stat:statement {hspace}
    | {hspace} stat:statement {hspace}
    ;

statement
    =
    | assignment
    | expression
    ;

where_conditions::WhereConditions
    = {hspace} value+:where_conditions {separator_with_space}+ value+:where_condition
    | {hspace} value+:where_condition {hspace}
    ;

where_condition::WhereCondition
    = id:identifier {hspace} ':' {hspace} type:la_type { {hspace} ':' {hspace} desc:description}
    ;

matrix_type::MatrixType
    = 'matrix' {hspace} '(' {hspace} id1:(integer | identifier)  {hspace} ',' {hspace} id2:(integer | identifier) {hspace}')'  {{hspace}+ attr+:matrix_attribute}
    | type:('ℝ'|'ℤ') {hspace} '^' {hspace} '(' {hspace} id1:(integer | identifier)  {hspace} '×' {hspace} id2:(integer | identifier) {hspace}')'  {{hspace}+ attr+:matrix_attribute}
    ;

matrix_attribute
    = SYMMETRIC
    | DIAGONAL
    ;

vector_type::VectorType
    = 'vector' {hspace} '(' {hspace} id1:(integer | identifier) {hspace}')'
    | type:('ℝ'|'ℤ') {hspace} '^' {hspace} '(' {hspace} id1:(integer | identifier) {hspace}')'
    | type:('ℝ'|'ℤ') {hspace} '^' {hspace} id1:(integer | identifier)
    ;

scalar_type::ScalarType
    = 'scalar'
    | 'ℝ'
    | z:'ℤ'
    ;

set_type::SetType
    = '{' {hspace} type+:('ℝ'|'ℤ') {hspace} {'×' {hspace} type+:('ℝ'|'ℤ') {hspace} }'}'
    | '{' {hspace} type1:('ℝ'|'ℤ') {hspace} '^' {hspace} cnt:(integer) {hspace} '}'
    | '{' {hspace} type2:('ℝ²'|'ℤ²') {hspace} '}'
    ;

la_type
    =
    | function_type
    | matrix_type
    | vector_type
    | set_type
    | scalar_type
    ;

params_type
    = matrix_type
    | vector_type
    | scalar_type
    ;

function_type::FunctionType
    = ((params+:params_type {{hspace} params_separator {hspace} params+:params_type})|'∅'|'{'{hspace}'}') {hspace} ('→'|'->') {hspace} ret:params_type
    ;

expression::Expression
    =
    | value:addition
    | value:subtraction
    | value:add_sub_operator
    | sign:['-'] value:term
    #| {}
    ;

assignment::Assignment
    =
    left:identifier {hspace} op:'=' {hspace} {separator_with_space} right:expression {hspace}
    | left:identifier {hspace} op:'+=' {hspace} {separator_with_space} right:expression {hspace}
    ;

term
    =
    | multiplication
    | division
    | factor
    ;

factor::Factor
    =
    | op:operations
    | sub:subexpression
    | nm:number_matrix
    | id:identifier
    | num:number
    | m:matrix
    | s:sparse_matrix
    ;

subexpression::Subexpression
    =
    '(' {hspace} value:expression {hspace} ')'
    ;

if_condition::IfCondition
    =
    | cond:not_equal
    | cond:equal
    | cond:in
    | cond:not_in
    | cond:greater
    | cond:greater_equal
    | cond:less
    | cond:less_equal
    ;

in::InCondition
    = '(' {hspace} left+:expression {hspace} {',' {hspace} left+:expression {hspace}} ')' {hspace} '∈' {hspace} right:identifier
    | left+:expression {hspace} '∈' {hspace} right:identifier
    ;

not_in::NotInCondition
    = '(' {hspace} left+:expression {hspace} {',' {hspace} left+:expression {hspace}} ')' {hspace} '∉' {hspace} right:identifier
    | left+:expression {hspace} '∉' {hspace} right:identifier
    ;

not_equal::NeCondition
    = left:expression {hspace} ('≠' | '!=') {hspace} right:expression
    ;

equal::EqCondition
    = left:expression {hspace} ('==' | '=') {hspace} right:expression
    ;

greater::GreaterCondition
    = left:expression {hspace} '>' {hspace} right:expression
    ;

greater_equal::GreaterEqualCondition
    = left:expression {hspace} ('>=' | '⩾') {hspace} right:expression
    ;

less::LessCondition
    = left:expression {hspace} '<' {hspace} right:expression
    ;

less_equal::LessEqualCondition
    = left:expression {hspace} ('<=' | '⩽') {hspace} right:expression
    ;
