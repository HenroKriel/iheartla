#operators
operations
    =
    | trace_operator
    | eig_operator
    | derivative_operator
    | inner_product_operator
    | matrix_operator
    | power_operator
    | sum_operator
    | det_operator
    | trans_operator
    | solver_operator
    ;

addition::Add
    =
    left:expression {hspace} op:'+' {hspace} right:term
    ;


subtraction::Subtract
    =
    left:expression {hspace} op:'-' {hspace} right:term
    ;


multiplication::Multiply
    =
    left:term {hspace} op:'*' {hspace} right:factor
    | left:term {hspace} right:factor
    ;


division::Divide
    =
    left:term {hspace} op:'/' {hspace} right:factor
    ;


dot_multiplication
    = identifier '.*' identifier
    ;


trace_operator
    = TRACE '(' {hspace} identifier {hspace} ')'
    ;


eig_operator
    = EIG '(' {hspace} identifier {hspace} ')'
    ;


derivative_operator::Derivative
    = DERIVATIVE value:identifier
    ;


power_operator::Power
    = base:identifier {hspace} t:'^T'
    | base:identifier {hspace} r:'^(-1)'
    | base:identifier {hspace} '^' {hspace} power:factor
    ;

solver_operator::Solver
    = left:identifier {hspace} '\' {hspace} right:identifier
    ;

sum_operator::Summation
    = SUM '_' sub:identifier_alone {hspace}+ exp:expression
    | SUM '_{' {hspace} id:identifier_alone {hspace} 'for' {hspace} cond:if_conditions {hspace} '}' {hspace}+ exp:expression
    ;

det_operator::Determinant
    = '|' {hspace} value:expression {hspace} '|'
    ;

inner_product_operator::InnerProduct
    = '<' {hspace} left:identifier {hspace} ',' {hspace}  right:identifier {hspace} '>'
    ;


matrix_operator
    = identifier '_' identifier
    | identifier '_' desc_identifier
    ;

trans_operator::Transpose
    = f:factor 'áµ€'
    ;