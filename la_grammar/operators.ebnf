#operators
operations
    =
    | trace_operator
    | eig_operator
    | derivative_operator
    | inner_product_operator
    | matrix_operator
    | power_operator
    | sum_operator
    | det_operator
    | trans_operator
    ;

addition::Add
    =
    left:term {hspace} op:'+' {hspace} right:expression
    ;


subtraction::Subtract
    =
    left:term {hspace} op:'-' {hspace} right:expression
    ;


multiplication::Multiply
    =
    left:term {hspace} op:'*' {hspace} right:term
    | left:term {hspace} right:term
    ;


division::Divide
    =
    left:term {hspace} op:'/' {hspace} right:factor
    ;


dot_multiplication
    = identifier '.*' identifier
    ;


trace_operator
    = TRACE '(' {hspace} identifier {hspace} ')'
    ;


eig_operator
    = EIG '(' {hspace} identifier {hspace} ')'
    ;


derivative_operator::Derivative
    = DERIVATIVE value:identifier
    ;


power_operator
    = identifier {hspace} '^' {hspace} identifier
    ;


sum_operator::Summation
    = SUM '_' sub+:identifier {',' sub+:identifier} {hspace}+ exp:expression
    ;

det_operator::Determinant
    = '|' {hspace} value:expression {hspace} '|'
    ;

inner_product_operator::InnerProduct
    = '<' {hspace} left:identifier {hspace} ',' {hspace}  right:identifier {hspace} '>'
    ;


matrix_operator
    = identifier '_' identifier
    | identifier '_' desc_identifier
    ;

trans_operator::Transpose
    = f:factor 'áµ€'
    ;