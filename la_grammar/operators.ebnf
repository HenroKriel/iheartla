# operators
operations
    =
    | function_operator
    | norm_operator
    | trace_operator
    | eig_operator
    #| derivative_operator
    | inner_product_operator
    | power_operator
    | sum_operator
    | optimize_operator
    | integral_operator
    | trans_operator
    | solver_operator
    | builtin_operators
    ;


addition::Add
    =
    left:expression {hspace} op:'+' {hspace} right:term
    ;


subtraction::Subtract
    =
    left:expression {hspace} op:'-' {hspace} right:term
    ;

add_sub_operator::AddSub
    =
    left:expression {hspace} op:('+-'|'±') {hspace} right:term
    ;


multiplication::Multiply
    =
    left:term {hspace} op:'*' {hspace} right:factor
    | left:term {hspace} right:factor
    ;


division::Divide
    =
    left:term {hspace} op:'/' {hspace} right:factor
    ;


dot_multiplication
    = identifier '.*' identifier
    ;


trace_operator
    = TRACE '(' {hspace} identifier {hspace} ')'
    ;


eig_operator
    = EIG '(' {hspace} identifier {hspace} ')'
    ;


derivative_operator::Derivative
    = DERIVATIVE value:identifier
    ;


power_operator::Power
    = base:factor {hspace} t:'^T'
    | base:factor {hspace} r:'^(-1)'
    | base:factor {hspace} '^' {hspace} power:factor
    ;

solver_operator::Solver
    = left:identifier {hspace} '\' {hspace} right:identifier
    ;

sum_operator::Summation
    = SUM '_' sub:identifier_alone {hspace}+ exp:expression
    | SUM '_(' {hspace} id:identifier_alone {hspace} 'for' {hspace} cond:if_condition {hspace} ')' {hspace}+ exp:expression
    ;

optimize_operator::Optimize
    = (min:MIN|max:MAX|amin:ARGMIN|amax:ARGMAX) '_' id:identifier_alone {hspace}+ exp:expression {{hspace} {separator} {hspace}} SUBJECT_TO {{hspace} {separator} {hspace}} cond:multi_cond
    ;

multi_cond::MultiCond
    = {hspace} m_cond:multi_cond {separator_with_space}+ cond:if_condition {hspace}
    | {hspace} cond:if_condition {hspace}
    ;

integral_operator::Integral
    = (INT|'∫') '_' (d:domain | (lower:(integer|identifier_alone) {hspace} '^' {hspace} upper:(integer|identifier_alone) )) {hspace} exp:expression {hspace} ('d'|'∂') id:identifier_alone
    ;

domain::Domain
    = '[' {hspace} lower:(integer|identifier_alone) {hspace} ',' {hspace} upper:(integer|identifier_alone) ']'
    ;

norm_operator::Norm
    = (double:'||' {hspace} value:expression {hspace} '||'
    | double:'‖' {hspace} value:expression {hspace} '‖'
    | single:'|' {hspace} value:expression {hspace} '|')
    [
    ( '_' sub:(integer|'F'|'*'|'∞'|identifier_alone) '^' power:factor)
    | ( '_' sub:(integer|'F'|'*'|'∞'|identifier) )
    | ( '_(' sub:(integer|'F'|'*'|'∞'|identifier) ')' ['^' power:factor])
    | ( '^' power:factor ['_' sub:(integer|identifier|'*'|'∞')] )
    ]
    ;

inner_product_operator::InnerProduct
    = '<' {hspace} left:identifier {hspace} ',' {hspace}  right:identifier {hspace} '>'
    ;


trans_operator::Transpose
    = f:factor 'ᵀ'
    ;

function_operator::Function
    = name:identifier '(' {{hspace} params+:expression {{hspace} params_separator {hspace} params+:expression}} {hspace}')'
    ;

builtin_operators
    =
    | sin_func
    | asin_func
    | cos_func
    | acos_func
    | tan_func
    | atan_func
    | atan2_func
    | sinh_func
    | asinh_func
    | cosh_func
    | acosh_func
    | tanh_func
    | atanh_func
    | cot_func
    | sec_func
    | csc_func
    | exp_func
    | log_func
    | ln_func
    | sqrt_func
    ;

sin_func::SinFunc
    = SIN {'^' power:integer} '(' {hspace} param:expression {hspace} ')'
    ;

asin_func::AsinFunc
    = (ASIN|ARCSIN) {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

cos_func::CosFunc
    = COS {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

acos_func::AcosFunc
    = (ACOS|ARCCOS) {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

tan_func::TanFunc
    = TAN {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

atan_func::AtanFunc
    = (ATAN|ARCTAN) {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

sinh_func::SinhFunc
    = SINH {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

asinh_func::AsinhFunc
    = (ASINH|ARSINH) {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

cosh_func::CoshFunc
    = COSH {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

acosh_func::AcoshFunc
    = (ACOSH|ARCOSH) {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

tanh_func::TanhFunc
    = TANH {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

atanh_func::AtanhFunc
    = (ATANH|ARTANH) {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

cot_func::CotFunc
    = COT {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

sec_func::SecFunc
    = SEC {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

csc_func::CscFunc
    = CSC {'^' power:integer}  '(' {hspace} param:expression {hspace} ')'
    ;

atan2_func::Atan2Func
    = ATAN2 '(' {hspace} param:expression {hspace} params_separator {hspace} second:expression {hspace} ')'
    ;

exp_func::ExpFunc
    = EXP '(' {hspace} param:expression {hspace} ')'
    ;

log_func::LogFunc
    = LOG '(' {hspace} param:expression {hspace} ')'
    ;

ln_func::LnFunc
    = LN '(' {hspace} param:expression {hspace} ')'
    ;

sqrt_func::SqrtFunc
    = SQRT '(' {hspace} param:expression {hspace} ')'
    ;